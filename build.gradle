plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'me.qoomon.git-versioning' version '6.3.0'
}

group 'org.tealeaf'

version="0.0.0-SNAPSHOT"
gitVersioning.apply {
    refs {
        branch('.+') {
            version = '${ref}-SNAPSHOT'
        }
        tag('v(?<version>.*)') {
            version = '${ref.version}'
        }
    }

    // optional fallback configuration in case of no matching ref configuration
    rev {
        version = '${commit}'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.9.0")

}

publishing {
    publications {
        githubPackages(MavenPublication) {
            groupId 'org.tealeaf'
            artifactId 'javamarkdown'

            from components.java

        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/LittleTealeaf/JavaMarkdown"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

javadoc {
    /*
    https://docs.gradle.org/current/javadoc/org/gradle/external/javadoc/StandardJavadocDocletOptions.html
     */
    options.showAll()
    options.setMemberLevel(JavadocMemberLevel.PACKAGE)
    var standardOptions = (StandardJavadocDocletOptions) options
    standardOptions.setAuthor(true);
    standardOptions.setHeader("View Source on <a href=\"https://www.github.com/LittleTealeaf/JavaMarkdown\">GitHub</a>")

    //standardOptions.setStylesheetFile("")
}


test {
    useJUnitPlatform()
}